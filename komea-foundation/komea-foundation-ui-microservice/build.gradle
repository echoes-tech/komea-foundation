
project.description = "Komea Software Factory : Komea Foundation"

buildscript {
    repositories {
      mavenLocal()
      mavenCentral()
      maven {
        url "https://plugins.gradle.org/m2/"
      }        
    }
    dependencies {
      classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
      classpath("gradle.plugin.com.craigburke.gradle:bower-installer-gradle:2.5.1")
    }
}

apply plugin: 'war'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'com.craigburke.bower-installer'

/* Install Bower dependencies during build */
build.dependsOn bowerInstall

/* Clean Bower dependencies during a Gradle Clean task */
clean.dependsOn bowerClean

jar {
    baseName = 'komea-foundation-gui'
    version = ''
}

war {
  baseName = 'komea-foundation'
}

configurations { providedRuntime }

springBoot {
    mainClass = "fr.echoes.labs.ksf.cc.KomeaFoundationGuiApplication"
}

dependencies {

  compile project(":ksf-commons:ksf-backend-microservice")
  compile project(":komea-foundation:komea-foundation-softwarefactory")
  compile project(":komea-foundation:komea-foundation-plugin-api")

  // Plugins preloaded
  compile project(":komea-foundation:komea-foundation-plugins:komea-foundation-foreman-plugin")
  compile project(":komea-foundation:komea-foundation-plugins:komea-foundation-puppet-plugin")
  compile project(":komea-foundation:komea-foundation-plugins:komea-foundation-jenkins-plugin")
  compile project(":komea-foundation:komea-foundation-plugins:komea-foundation-redmine-plugin")
  compile project(":komea-foundation:komea-foundation-plugins:komea-foundation-git-plugin")
  compile project(":komea-foundation:komea-foundation-plugins:komea-foundation-nexus-plugin")

  compile("org.springframework.boot:spring-boot-starter-thymeleaf:${springBootVersion}")
  providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")

  // Test Frameworks
  testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")

}

bower {

  installBase = 'src/main/resources/static/assets/'

  'bootstrap'('3.3.2') {
    source 'dist/**'
  }

  'jquery'('2.0.2')
  
  'jquery-resize'('https://github.com/sdecima/javascript-detect-element-resize.git')
  'components-font-awesome'('4.3.0')
  'ionicons'('2.0.1')
  'zeroclipboard'('2.2.0')

  'salvattore'('1.0.9')
  
  'jquery-treegrid'('0.3.0') {
	  source 'js/**'
	  source 'css/**'
  }
}
